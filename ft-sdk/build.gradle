apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


def versionNameStr="1.0.3-alpha20"
def min_plugin_version ="1.0.1-alpha05"

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 65
        versionName versionNameStr

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "FT_SDK_VERSION", "\"${versionNameStr}\"")
            buildConfigField("String", "MIN_FT_PLUGIN_VERSION", "\"${min_plugin_version}\"")
        }

        debug {
            buildConfigField("String", "FT_SDK_VERSION", "\"${versionNameStr}\"")
            buildConfigField("String", "MIN_FT_PLUGIN_VERSION", "\"${min_plugin_version}\"")
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:android-all:10-robolectric-5803371'
    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation 'org.robolectric:shadows-httpclient:3.3.2'
    testImplementation 'com.google.mockwebserver:mockwebserver:20130706'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha03'
    androidTestImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    implementation 'com.google.android.material:material:1.1.0'
    compileOnly 'com.squareup.okhttp3:okhttp:4.5.0'
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        publications {
            mavenAar(MavenPublication) {
                groupId 'com.cloudcare.ft.mobile.sdk.tracker.agent'
                artifactId 'ft-sdk'
                version versionNameStr
                artifact(sourceJar)
                artifact("$buildDir/outputs/aar/ft-sdk-release.aar"){
                    builtBy assemble
                }
            }
        }
        repositories {
            maven {

                def releasesRepoUrl = "$maven_publish_url/maven-releases"
                def snapshotsRepoUrl = "$maven_publish_url/maven-snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = getDefaultProperties().get("LDAP_ACCOUNT")
                    password = getDefaultProperties().get("LDAP_PWD")
                }
            }


        }
    }
}


task generateAarAndPublish{
    dependsOn 'clean'
    dependsOn 'publish'
}



