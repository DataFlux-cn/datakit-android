apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

//发布 snapshot 版本号末尾追加 "-SNAPSHOT"
def versionNameStr = "1.4.6-alpha01"

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "FT_SDK_VERSION", "\"${versionNameStr}\"")

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    namespace 'com.ft.sdk'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:android-all:10-robolectric-5803371'
    testImplementation 'org.robolectric:robolectric:4.7.2'
    testImplementation 'org.robolectric:shadows-httpclient:3.3.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    testImplementation 'com.google.mockwebserver:mockwebserver:20130706'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation project(':ft-test')
    androidTestImplementation 'com.squareup.okhttp3:okhttp:4.5.0'
    androidTestImplementation 'com.google.mockwebserver:mockwebserver:20130706'
    androidTestImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    compileOnly 'com.google.android.material:material:1.1.0'
    compileOnly 'com.squareup.okhttp3:okhttp:4.5.0'
    compileOnly project(":ft-native")
}


afterEvaluate {
    publishing {
        publications {
            mavenAar(MavenPublication) {
                groupId 'com.cloudcare.ft.mobile.sdk.tracker.agent'
                artifactId 'ft-sdk'
                version versionNameStr
                artifact project.tasks.bundleReleaseAar
                // 添加源码Javadoc到发布的AAR文件中
                artifact androidJavadocsJar
                artifact androidSourcesJar

            }
        }
        repositories {
            maven {

                def releasesRepoUrl = "$maven_publish_url/maven-releases"
                def snapshotsRepoUrl = "$maven_publish_url/maven-snapshots"
                url = versionNameStr.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = getDefaultProperties().get("LDAP_ACCOUNT")
                    password = getDefaultProperties().get("LDAP_PWD")
                }
            }


        }
    }
}


task generateAarAndPublish {
    dependsOn 'clean'
    dependsOn 'publish'
}

task aar {
    dependsOn 'assemble'

}

/**
 * 报错，不影响上传任务
 */
task androidJavadocs(type: Javadoc) {

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            //BuildConfig Class Not Found
            source += variant.javaCompileProvider.get().source
            //Dependencies  Library Class Not Found
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
    //ignore doc lint
    options.addStringOption('Xdoclint:none', '-quiet')
    //ignore build fail
    failOnError false
}

task androidJavadocsJar(type: Jar) {
    dependsOn androidJavadocs
    archiveClassifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}


